import org.apache.tools.ant.taskdefs.condition.Os

buildscript {
	repositories {
		mavenLocal()
		mavenCentral()
		jcenter()
	}
	dependencies {
		classpath 'io.codearte.gradle.nexus:gradle-nexus-staging-plugin:0.9.0'
	}
}

apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: "java"
apply plugin: 'maven'
apply plugin: 'signing'

if(project.hasProperty('ossrhUser') && project.hasProperty("release")) {
	apply plugin: "io.codearte.nexus-staging"

	nexusStaging {
		username = ossrhUser
		password = ossrhPassword
	}
}

group = 'com.elefana'
version = '1.0.0-SNAPSHOT'
description = 'Utility classes for implementing Elasticsearch compatibility'

repositories {
	mavenLocal()
	mavenCentral()
}

ext {
	jodaVersion = '2.9.9'
}

sourceCompatibility = 1.7
targetCompatibility = 1.7

task javadocJar(type: Jar) {
	classifier = 'javadoc'
	from javadoc
}

task sourcesJar(type: Jar) {
	classifier = 'sources'
	from sourceSets.main.allSource
}

artifacts {
	archives javadocJar, sourcesJar
}

dependencies {
	compile "joda-time:joda-time:$jodaVersion"
	
	testCompile 'junit:junit:4.11'
	testCompile "org.jmock:jmock-junit4:2.5.1"
	testCompile "org.jmock:jmock-legacy:2.5.1"
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.3'
}

// Maven Central publication
if(project.hasProperty('ossrhUser')) {
	signing {
		sign configurations.archives
	}
	
	uploadArchives {
		repositories {
			mavenDeployer {
				// POM signature
				beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
				// Target repository
				repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
					authentication(userName: ossrhUser, password: ossrhPassword)
				}
				pom.project {
					name project.name
					description project.description
					packaging 'jar'
					url 'https://github.com/elefana/es-compat-utils'

					scm {
						connection 'scm:git:https://github.com/elefana/es-compat-utils.git'
						developerConnection 'scm:git:git@github.com:elefana/es-compat-utils.git'
						url 'https://github.com/elefana/es-compat-utils.git'
					}

					licenses {
						license {
							name 'Apache License 2.0'
							url 'https://opensource.org/licenses/Apache-2.0'
							distribution 'repo'
						}
					}

					developers {
						developer {
							id = 'tomcashman'
							name = 'Thomas Cashman'
							email = 'thomascashman404@gmail.com'
						}
					}
				}
			}
		}
	}
}